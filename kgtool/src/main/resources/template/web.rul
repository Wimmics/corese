<?xml version='1.0' encoding='UTF-8'?>
<!--
SPARQL Template Transformation
Olivier Corby - Wimmics - Inria UNS CNRS I3S
Tue Jan 05 14:25:23 CET 2016
-->
<rdf:RDF  xmlns:rdf='http://www.w3.org/1999/02/22-rdf-syntax-ns#' 
  xmlns='http://ns.inria.fr/edelweiss/2011/rule#'>
<rule>
<body>
<![CDATA[
#
# SPARQL Tutorial
# st:get(st:uri) is the name of a query to execute (st:minus)
# It is used 1) to select a query and 2) to display the result of the query
# RDF graph is either RDF dataset or Query Result
# Query definitions are stored in st:context named graph 
#
template st:start {

  st:call-template-with(st:server, st:head)

  st:call-template(st:head)
    
  st:call-template(st:select, ?uri)
  
  st:call-template(st:display, ?uri)
   
  st:call-template(st:tail)

  st:call-template-with(st:server, st:tail)
}
where {

  bind (coalesce(st:get(st:uri), st:intro) as ?uri)
  
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:content2(?t, ?p) {

  str(?t) "<br/>"    group distinct { "<a href='" str(?d) "'>" str(?d) "</a>" ; separator="<br/>"} "<br/>"
 
}
where {
  bind (st:get(st:context) as ?g)
  graph ?g {
    ?ds a st:Dataset
    ?ds ?p [ st:uri ?d ]
  }
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:content {
  
  st:call-template(st:content2, "Data:",   st:data)
  st:call-template(st:content2, "Schema:", st:schema)
  st:call-template(st:content2, "Query:",  st:context)
}
where {

}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:tail {
  
  """ 
  <h3>Dataset</h3>
  """
  
  st:call-template(st:content) 
  
  """ 
  <p> <hr/> </p>
  """
  
  

}
where {

}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:next(?t) {

  "<a  href='" st:plink(?n) "'>"
  "<button type='button'"  st:call-template-with(st:server, st:bstyle)
  ">Next</button>"
  "</a>"  
    
}
where {
  bind (st:get(st:context) as ?g)
  
  graph ?g {
    ?q1 st:name ?t ; st:index ?i
    bind (?i + 1 as ?j)
    ?q2 st:index ?j ;
	st:name ?n 
  }
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:profile {
   
}
where {}
 
  function st:proto() {
    coalesce(st:get(st:protocol), st:std)
  }


]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Display result of query
# RDF graph is a result graph
#

template st:result {

   st:call-template-with(st:sparql, st:result, st:get(st:service), st:sparql)
    
}
where {
  bind (st:get(st:query) as ?query)
  filter bound(?query)
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:prev(?t) {

  "<a  href='" st:plink(?n) "'>"
  "<button type='button'"  st:call-template-with(st:server, st:bstyle)       
  ">Previous</button>"
  "</a>"  
    
}
where {
  bind (st:get(st:context) as ?g)
  
  graph ?g {
    ?q1 st:name ?t ; st:index ?i
    bind (?i - 1 as ?j)
    ?q2 st:index ?j ;
	st:name ?n 
  }
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Generate a form with a SPARQL query ?q to submit
# Argument  ?n is the name of query (st:minus)
# ?q actual query, ?t query template, ?s query solution
#
template st:form(?n, ?q, ?t, ?s) {
  """
   <script type="text/javascript">           
        function show(name){
            document.getElementById('query').value = document.getElementById(name).value;
        }                
   </script>
       
   <form method='post'  id='form' 	action='"""st:get(st:service)"""' >   
   <input type='hidden' id='tmp' 	value='"""str(?t)"""' />
   <input type='hidden' id='sol' 	value='"""str(?s)"""' />
   <input type='hidden' name='uri' 	value='"""?n"""'/>
   <input type='hidden' name='profile'  value='"""st:get(st:profile)"""'/> 
   """
   
   if (?s = st:null, "", st:call-template(st:nav))
      
   "<input type='submit' value='submit' "  
     st:call-template-with(st:server, st:submit)  
   "/>"
   
   """
   <br/>
   <textarea cols='60' rows='10' id='query' name='query'>"""str(?q)"""</textarea>
   </form>
   """
}
where {
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Select a query name to display and execute
# query name bound to uri param
# Generate query names list from context graph
#
template st:select(?t) {

"""
<h3>Select a query</h3>

<form method='get' id ='select' action='""" st:get(st:service) """'>

  <input type='hidden' name='profile' value='""" st:get(st:profile) """' />
"""

  st:call-template(st:prev, ?t)

  "<select " st:call-template-with(st:server, st:sstyle)   
    " id='uri' name='uri' onChange='"   
     st:call-template-with(st:server, st:change)    
  "'>"
  
    st:call-template(st:list, ?t)  
    
  "</select>" 

  st:call-template(st:next, ?t)
 
""" 
</form>
"""

}
where {
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:nav {
 
 "<input type='button' " st:call-template-with(st:server, st:bstyle) """ 
      onclick="show('sol')" value='Solution' />
      
  <input type='button' """ st:call-template-with(st:server, st:bstyle) """ 
      onclick="show('tmp')" value='Template' /> """
   
 }
where {}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# ?n is the st:name of a query
# get definition of query in context graph
# display query and eventually result of query
# st:get(st:query) is user query
# ?q is tutorial query
#
template st:display(?n) {

"<h3>" str(?l) "</h3>"
"<p>"  
  str(?c) 
  if (bound(?r), 
    concat("<br/><a href='", str(?r), "'>(", str(?r), ")</a>"), "")
"</p>"

   st:call-template(st:form, ?n,
     coalesce(st:get(st:query), str(?q)), str(?q), coalesce(?s, st:null))
   
   st:call-template(st:result)
      
}
where {
  # named graph that contains definition of queries
  bind (st:get(st:context) as ?g)
  graph ?g {
    ?qt st:name    ?n ;
      rdfs:label   ?l ;
      rdfs:comment ?c ;
      st:query     ?q
      optional { ?qt st:reference ?r }
      optional { ?qt st:solution  ?s }
      filter(lang(?c) = (coalesce(st:get(st:lang), "fr")))
  }
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Generate query names list from context graph
#
template st:list(?t) {

  "<option value='" ?q "'" 
    if (?q = ?t, "selected='true'", "")
  ">" 
  str(?i) ". " str(?title) 
  "</option>" 
   
}
where {
  bind (st:get(st:context) as ?g)
  graph ?g {
    [] a st:Query ;
      st:index ?i ;
      st:name ?q ;
      rdfs:label ?title
  }
}
order by ?i

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:head {
   
  "<h2 class='center'>SPARQL Tutorial</h2>"
  
}
where {

}

]]>
</body>
</rule>

</rdf:RDF>
