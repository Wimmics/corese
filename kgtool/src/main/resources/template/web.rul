<?xml version='1.0' encoding='UTF-8'?>
<!--
SPARQL Template Transformation
Olivier Corby - Wimmics - Inria UNS CNRS I3S
Thu Feb 23 09:12:10 CET 2017
-->
<rdf:RDF  xmlns:rdf='http://www.w3.org/1999/02/22-rdf-syntax-ns#'
  xmlns='http://ns.inria.fr/sparql-template/'>
<rule>
<body>
<![CDATA[
#
# SPARQL Tutorial
# st:get(st:uri) is the name of a query to execute (st:minus)
# It is used 1) to select a query and 2) to display the result of the query
# RDF graph is either RDF dataset or Query Result
# Query definitions are stored in st:context named graph 
#
template st:start {

  st:head()

  st:call-template(st:head)
    
  st:call-template(st:selectform, ?uri)
  
  st:call-template(st:display, ?uri)
   
  st:call-template(st:tail)

  st:tail()
}
where {

  bind (coalesce(st:get(st:uri), st:intro) as ?uri)

}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:content {
  
  st:call-template(st:content2, "Data:",   st:data)
  st:call-template(st:content2, "Schema:", st:schema)
  st:call-template(st:content2, "Query:",  st:context)
}
where {

}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Display result of query
# RDF graph is a result graph
#

prefix spq: <http://ns.inria.fr/sparql-template/sparql#>
template st:result {

   st:apply-templates-with(?sttl)
    
}
where {
  bind (st:get(st:query) as ?query)
  bind (coalesce(st:get(st:sttl), spq:result)  as ?sttl)
  filter bound(?query)
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:tail {
format {
  """ 
  <h3>Dataset</h3>
  %s
  <p> <hr/> </p>
  """
  
  st:call-template(st:content)
}
}
where {

}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:prev(?t) {

format {
  """
  <a  href='%s'>
  <button type='button' %s>Previous</button>
  </a>
  """
  
  st:plink(?n)
  st:bstyle()
  }
  
}
where {
  bind (st:get(st:context) as ?g)
  
  graph ?g {
    ?q1 st:name ?t ; st:index ?i
    bind (?i - 1 as ?j)
    ?q2 st:index ?j ;
	st:name ?n 
  }
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:next(?t) {

format {
  """
  <a  href='%s'>
  <button type='button' %s>Next</button>
  </a>
  """ 
  st:plink(?n)
  st:bstyle()
 }
    
}
where {
  bind (st:get(st:context) as ?g)
  
  graph ?g {
    ?q1 st:name ?t ; st:index ?i
    bind (?i + 1 as ?j)
    ?q2 st:index ?j ;
	st:name ?n 
  }
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:profile {
   
}
where {}
 
  function st:proto() {
    coalesce(st:get(st:protocol), st:std)
  }


]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Select a query name to display and execute
# query name bound to uri param
# Generate query names list from context graph
#
template st:selectform(?t) {

format {

  """
  <h3>Select a query</h3>
  <form method='get' id ='select' action='%s'>
  <input type='hidden' name='profile' value='%s' />
  %s   
  <select %s id='uri' name='uri' onChange='%s'>
  %s
  </select>   
  %s
  </form>
  """
  
  st:get(st:service) 
  st:get(st:profile)   
  st:call-template(st:prev, ?t)  
  st:sstyle() 
  st:change() 
  st:call-template(st:list, ?t)  
  st:call-template(st:next, ?t)
}
}
where {
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Generate a form with a SPARQL query ?q to submit
# Argument  ?n is the name of query (st:minus)
# ?q actual query, ?t query template, ?s query solution
#
template st:form(?n, ?q, ?t, ?s) {

   st:call-template(st:script)
   
   format {
   
    """
    <form method='post'  id='form' 	action='%s' >   
      <input type='hidden' name='profile' value='%s' />     
      <input type='hidden' id='tmp' 	value='%s' />
      <input type='hidden' id='sol' 	value='%s' />
      <input type='hidden' name='uri' 	value='%s' />
      %s
      <input type='submit' value='submit' %s/>
      <br/>
      <textarea cols='60' rows='10' id='query' name='query'>%s</textarea>
    </form>
    """  
   
    st:get(st:service) 
    st:get(st:profile)
    str(?t) 
    str(?s) 
    ?n 
    if (?s = st:null, "", st:call-template(st:nav))
    st:submit() 
    str(?q)
    }
   
}
where {
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:script {
   """
   <script type="text/javascript">           
        function show(name){
            document.getElementById('query').value = document.getElementById(name).value;
        }                
   </script>
   """
}
where {}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:nav {
 
 format {
 
 """
 <input type='button' %s onclick="show('sol')" value='Solution' />      
 <input type='button' %s onclick="show('tmp')" value='Template' /> 
 """
      
  st:bstyle()
  st:bstyle()   
  }
   
 }
where {}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Generate query names list from context graph
#
template st:list(?t) {

format {
  """
  <option value='%s'" %s> 
  %s . %s 
  </option>
  """
  
  ?q
  if (?q = ?t, "selected='true'", "")
  str(?i)
  str(?title)
}
   
}
where {
  bind (st:get(st:context) as ?g)
  graph ?g {
  values ?qt {st:Query sw:Query}
    [] a ?qt ;
      st:index ?i ;
      st:name ?q ;
      rdfs:label ?title
  }
}
order by ?i

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:content2(?t, ?p) {

  str(?t) "<br/>"  
  
  group distinct { 
    "<a href='" str(?d) "'>" str(?d) "</a>" 
    ; separator="<br/>"
  } 
  
  "<br/>"
 
}
where {
  bind (st:get(st:context) as ?g)
  graph ?g {
    ?ds a st:Dataset
    ?ds ?p [ st:uri ?d ]
  }
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:head {
   
  format {
  "<h2 class='center'>%s</h2>" 
  str(?title)
  }
  
}
where {
  bind (coalesce(st:get(st:title), "Tutorial") as ?title)
}

]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# ?n is the st:name of a query
# get definition of query in context graph
# display query and eventually result of query
# st:get(st:query) is user query
# ?q is tutorial query
#
template st:display(?n) {

format {
  """
  <h3>%s</h3>
  <p>  
  %s
  %s
  </p>
  %s
  %s
  """

   str(?l)
   str(?c)   
   if (bound(?r), st:format("<br/><a href='%s'>(%s)</a>", str(?r), str(?r)), "")
   
   st:call-template(st:form, 
     ?n,
     coalesce(st:get(st:query), str(?q)), 
     str(?q), 
     coalesce(?s, st:null)) 
     
   st:call-template(st:result)
 }
      
}
where {
  # named graph that contains definition of queries
  bind (st:get(st:context) as ?g)
  graph ?g {
    ?qt st:name    ?n ;
      rdfs:label   ?l ;
      rdfs:comment ?c ;
      st:query     ?q
      optional { ?qt st:reference ?r }
      optional { ?qt st:solution  ?s }
      optional { ?qt sw:body  ?s }
      filter(lang(?c) = (coalesce(st:get(st:lang), "fr")))
  }
}

]]>
</body>
</rule>

</rdf:RDF>
